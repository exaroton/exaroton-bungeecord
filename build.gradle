import java.nio.file.Files
import java.nio.file.Paths
import java.nio.file.StandardCopyOption

plugins {
    id 'java-library'
    id 'com.github.johnrengelman.shadow' version '7.0.0'
    id 'maven-publish'
    id 'signing'
}

group 'com.exaroton'
version '1.5.3-SNAPSHOT'

repositories {
    maven { url 'https://oss.sonatype.org/content/repositories/snapshots' }
    mavenCentral()
}

dependencies {
    shadow group: 'net.md-5', name: 'bungeecord-api', version: '1.16-R0.5-SNAPSHOT'
    implementation 'com.exaroton:api:1.4.1'
}

ext.isReleaseVersion = !version.endsWith("SNAPSHOT")

java {
    withJavadocJar()
    withSourcesJar()
}

publishing {
    repositories {
        maven {
            def releaseRepo = "https://s01.oss.sonatype.org/service/local/staging/deploy/maven2/"
            def snapshotRepo = "https://s01.oss.sonatype.org/content/repositories/snapshots/"
            url = isReleaseVersion ? releaseRepo : snapshotRepo
            credentials {
                username = project.hasProperty('ossrhUsername') ? ossrhUsername : "Unknown user"
                password = project.hasProperty('ossrhPassword') ? ossrhPassword : "Unknown password"
            }
        }
    }

    publications {
        mavenJava(MavenPublication) {
            from components.java
            artifactId = 'bungee'
            pom {
                name = 'exaroton Bungee API'
                description = 'The official exaroton bungee plugin to manage exaroton servers in your proxy'
                url = 'https://github.com/exaroton/exaroton-bungeecord'
                licenses {
                    license {
                        name = 'MIT'
                        url = 'https://github.com/exaroton/exaroton-bungeecord/blob/master/LICENSE'
                    }
                }
                developers {
                    developer {
                        id = 'julian'
                        name = 'Julian Vennen'
                        email = 'julian@aternos.org'
                    }
                }
                scm {
                    connection = 'scm:git:https://github.com/exaroton/exaroton-bungeecord.git'
                    developerConnection = 'scm:git:git@github.com:exaroton/exaroton-bungeecord.git'
                    url = 'https://github.com/exaroton/exaroton-bungeecord'
                }
            }
        }
    }
}

signing {
    sign publishing.publications.mavenJava
}
tasks.withType(Sign) {
    onlyIf { isReleaseVersion }
}

processResources {
    filter {
        it.replace('${project.version}', project.version)
    }
}

shadowJar {
    archiveClassifier.set('')
}

abstract class CopyPluginTask extends DefaultTask {
    @Input
    abstract Property<String> getArtifactName()

    @TaskAction
    def copyPlugin() {
        File runDir = new File("./run");
        if (!runDir.exists() || !runDir.isDirectory()) {
            println "Directory 'run' does not exist."
            return;
        }

        File pluginsDir = new File(runDir, "plugins");
        if (!pluginsDir.exists()) {
            pluginsDir.mkdir();
        }

        Files.copy(Paths.get("./build/libs/", getArtifactName().get()),
                pluginsDir.toPath().resolve("exaroton-bungee-dev.jar"),
                StandardCopyOption.REPLACE_EXISTING);
    }
}

tasks.register('copyPlugin', CopyPluginTask) {
    artifactName = "exaroton-bungee-${version}.jar"
}